def includeCrashlytics() {
    return (project.hasProperty("crashlyticsdemoApisecret")  && project.hasProperty("crashlyticsdemoApikey"))
}


buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
if (includeCrashlytics()) {
    apply plugin: 'io.fabric'
}
repositories {
    maven {url "http://levelupstudio.com/maven2"}
    maven { url 'https://maven.fabric.io/public' }
}


android {
    signingConfigs {
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    defaultConfig {
        applicationId "com.levelup.bazquxforpalabre"
        minSdkVersion 16
        targetSdkVersion 23
        versionName '0.8 alpha'
    }
    buildTypes {
        debug {
            // disable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // enable crashlytics
            if (includeCrashlytics()) {
                buildConfigField "boolean", "USE_CRASHLYTICS", "true"
                ext.enableCrashlytics = true
            } else {
                buildConfigField "boolean", "USE_CRASHLYTICS", "false"
                ext.enableCrashlytics = false

            }
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.koushikdutta.ion:ion:2.1.5'
    compile 'org.jsoup:jsoup:1.7.2'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.github.traex.rippleeffect:library:1.3'
    compile 'com.levelup.palabre.api:palabre-api:1.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.0@aar') {
        transitive = true;
    }
}



afterEvaluate {
    if (includeCrashlytics()) {
        initCrashlyticsPropertiesIfNeeded()
    }
}

def initCrashlyticsPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated crashlytics property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: crashlyticsdemoApisecret)
            entry(key: "apiKey", value: crashlyticsdemoApikey)
        }
    }
}
